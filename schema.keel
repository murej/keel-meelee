enum DaysInAWeek {
    Monday
    Tuesday
    Wednesday
    Thursday
    Friday
}

enum MealTime {
    Breakfast
    Lunch
    Dinner
}

model Meal {
    fields {
        day DaysInAWeek
        time MealTime
        recipe Text
        portions Number
        plan Plan
    }

    operations {
        get getMeal(id)
        list listMeals(day?)
        create createMeal() with (day, time, recipe, portions, plan.id)
    }

    @permission(
        actions: [get, list],
        expression: true
    )

    @permission(
        actions: [create, update],
        expression: ctx.isAuthenticated
    )
}

model Plan {
    fields {
        meals Meal[]
        mouthsToFeed Number
        weekNumber Number
        user User
    }

    operations {
        get getPlan(id)
        list listPlans()
        create createPlan() with (meals.id?, mouthsToFeed, weekNumber, user.id)
        update updatePlanMeal(id) with (
            meals.day,
            meals.time,
            meals.recipe,
            meals.portions,
        )
    }

    @permission(
        actions: [get, list],
        expression: true
    )

    @permission(
        actions: [create, update],
        expression: ctx.isAuthenticated
    )
}

model User {
    fields {
        identity Identity
        plans Plan[]
    }

    operations {
        create createUser() with (identity.id, plans.id?)
    }
}
